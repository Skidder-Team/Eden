plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'rip.diamond'
version = "1.1.28-BETA-" + getGitHash()

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileJava.options.encoding = 'UTF-8'

String getGitHash() {
    def output = new ByteArrayOutputStream()
    exec {
        standardOutput = output // Only the std, not errors as it's not to worry about as long as it's a repo
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
    }
    return output.toString().trim()
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dir("./libs")
    }

    //Spigot and ImanitySpigot API
    maven {
        url = "https://hub.spigotmc.org/nexus/repository/public/"
    }
    maven {
        name "imanityLibraries"
        url "https://maven.imanity.dev/imanity-libraries"
    }
    //PlaceholderAPI
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    //Citizens
    maven {
        name 'citizens-repo'
        url 'https://maven.citizensnpcs.co/repo'
    }
}

dependencies {
    implementation 'org.mongodb:mongodb-driver-sync:4.9.0' //MongoDB driver (This has to be included in the plugin jar file, so we use implementation)
    //implementation 'rip.diamond:spigotapi:1.0-SNAPSHOT'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.3' //https://github.com/ben-manes/caffeine - A high performance caching library

    testImplementation 'dev.imanity.mockbukkit:MockBukkit1.8:1.0.31'
    testImplementation 'junit:junit:4.13.1'

    //Spigot
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'

    //Libraries from local
    compileOnly name: 'ImanitySpigot-API-2022.08.1b2'
    compileOnly name: 'TacoSpigot-1.8.8'
    compileOnly name: 'FastAsyncWorldEdit-bukkit-18.12.19-d131fa6-1241-22.0.3'
    compileOnly name: 'worldedit-bukkit-6.1.9'
    compileOnly name: 'ViaVersion-4.4.2'

    //Libraries from others
    compileOnly 'me.clip:placeholderapi:2.11.7' //PlaceholderAPI
    compileOnly 'net.dmulloy2:ProtocolLib:5.4.0' //ProtocolLib
    compileOnly 'net.citizensnpcs:citizensapi:2.0.30-SNAPSHOT' //Citizens

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

test {
    useJUnit()
    maxHeapSize = '1G'
}

configurations {
    testCompile.extendsFrom compileOnly
}

tasks.withType(JavaCompile).configureEach {
    options.forkOptions.jvmArgs.addAll(['--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'])
}
